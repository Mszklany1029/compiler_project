/* Converts a roman numeral string into an integer */
let
        function printint(i: int) =
        let function f(i:int) = if i>0 
            	then (f(i/10); print(chr(i-i/10*10+ord("0"))))
        in
                if i<0
                then (print("-"); f(-i))
                else if i>0 then f(i) else print("0")
        end

	function charat(s: string, index: int) = if index > size(s) - 1 then "" else substring(s, index, 1)

        function romantoint_iter(s: string, sum: int) =
        if size(s) = 0 then sum
        else let
		var addend :=
		    if charat(s, 0) = "I" then
		         if charat(s, 1) = "V" | charat(s, 1) = "X" then -1 else 1
		    else if charat(s, 0) = "V" then 5
		    else if charat(s, 0) = "X" then
		    	 if charat(s, 1) = "L" | charat(s, 1) = "C" then -10 else 10
		    else if charat(s, 0) = "L" then 50
		    else if charat(s, 0) = "C" then
		    	 if charat(s, 1) = "D" | charat(s, 1) = "M" then -100 else 100
		    else if charat(s, 0) = "D" then 500
		    else if charat(s, 0) = "M" then 1000
		    else 0
	in
		romantoint_iter(substring(s, 1, size(s) - 1), sum + addend)	
	end


        function romantoint(input: string) = romantoint_iter(input, 0)

        var input := "MCMXCIV"
in
        printint(romantoint(input))
end
